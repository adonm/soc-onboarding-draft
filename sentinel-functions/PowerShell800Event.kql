// PowerShell 800 KQL Event Parser v1.0
// Last Updated Date: 2022-08-01
// Author: Jason Christian (DGov WA) https://github.com/jasonkasih
let EventData = Event
| where Source == "PowerShell" and EventID == 800
| extend RenderedDescription = tostring(split(RenderedDescription, ":")[0])
| project TimeGenerated,
    Source,
    EventID,
    Computer,
    UserName,
    EventData,
    RenderedDescription,
    ingestion_ = ingestion_time(), //Added DGov
    TenantId //Added DGov
| extend EvData = parse_xml(EventData)
| extend EventDetail = EvData.DataItem.EventData.Data
| project-away EventData, EvData;
EventData
| extend Pipeline_CommandLine = tostring(EventDetail[0]) //Loaded module
| extend EventData_Context = tostring(EventDetail[1])
| extend Details = tostring(EventDetail[2]) //Parameter binding details
| parse kind=simple EventData_Context with * 'DetailSequence=' DetailSequence "DetailTotal=" *
| parse kind=relaxed EventData_Context with * 'DetailTotal=' DetailTotal "SequenceNumber=" *
| parse kind=relaxed EventData_Context with * 'SequenceNumber=' SequenceNumber "UserId=" *
| parse kind=relaxed EventData_Context with * 'UserId=' UserId "HostName=" *
| parse kind=relaxed EventData_Context with * 'HostName=' HostName "HostVersion=" *
| parse kind=relaxed EventData_Context with * 'HostVersion=' HostVersion "HostApplication=" *
| parse kind=relaxed HostVersion with HostVersion "HostId=" *   //Fixes for HostVersion field non-standard delimiter with HostId
| parse kind=simple HostVersion with * 'HostId=' HostId         // Fixes for HostId field parsed in non-standard delimiter
| parse kind=relaxed EventData_Context with * 'HostApplication=' HostApplication "EngineVersion=" *
| parse kind=relaxed EventData_Context with * 'EngineVersion=' EngineVersion "RunspaceId=" *
| parse kind=relaxed EventData_Context with * 'RunspaceId=' RunspaceId "PipelineId=" *
| parse kind=relaxed EventData_Context with * 'PipelineId=' PipelineId "ScriptName=" *
| parse kind=relaxed EventData_Context with * 'ScriptName=' ScriptName "CommandLine=" *
| parse kind=simple EventData_Context with * 'CommandLine=' CommandLine //the last field parsed
| project-away EventData_Context ,EventDetail
